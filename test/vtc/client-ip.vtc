varnishtest "Test client-ip handling"

server s1 {
  rxreq
  txresp
} -start

varnish v1 -vcl+backend {
  sub vcl_deliver {
    ### Figure out which is the best public IP to use
    # Prefer fly-client-ip header
    if (req.http.fly-client-ip) {
      set resp.http.x-client-ip = req.http.fly-client-ip;
    # If the above is not present, take x-forwarded-for
    } else if (req.http.x-forwarded-for) {
      set resp.http.x-client-ip = regsub(req.http.x-forwarded-for, "^([^,]+).*", "\1");
    # If neither are present, use the default
    } else {
      set resp.http.x-client-ip = client.ip;
    }
  }
} -start

# Test 1: Test fly-client-ip header
client c1 {
  txreq -url "/" -hdr "fly-client-ip: 151.101.129.162"
  rxresp
  expect resp.status == 200
  expect resp.http.x-client-ip == "151.101.129.162"
} -run

# Test 2: fly-client-ip header is preferred when x-forwarded-for is set
client c1 {
  txreq -url "/" -hdr "fly-client-ip: 151.101.129.162" -hdr "x-forwarded-for: 151.101.1.162, 66.241.124.108, 172.16.5.82"
  rxresp
  expect resp.status == 200
  expect resp.http.x-client-ip == "151.101.129.162"
} -run

# Test 3: uses the first ip when 3 ips are present in x-forwarded-for
client c1 {
  txreq -url "/" -hdr "x-forwarded-for: 151.101.1.162, 66.241.124.108, 172.16.5.82"
  rxresp
  expect resp.status == 200
  expect resp.http.x-client-ip == "151.101.1.162"
} -run

# Test 4: uses the first ip when 2 ips are present in x-forwarded-for
client c1 {
  txreq -url "/"  -hdr "x-forwarded-for: 151.101.1.162, 66.241.124.108"
  rxresp
  expect resp.status == 200
  expect resp.http.x-client-ip == "151.101.1.162"
} -run

# Test 5: uses the ip in x-forwarded-for & it prefers it over the default client ip
client c1 {
  txreq -url "/" -hdr "x-forwarded-for: 151.101.1.162"
  rxresp
  expect resp.status == 200
  expect resp.http.x-client-ip == "151.101.1.162"
} -run

# Test 6: uses the default client ip
client c1 {
  txreq -url "/"
  rxresp
  expect resp.status == 200
  expect resp.http.x-client-ip == "127.0.0.1"
} -run
