varnishtest "Test news mp3 redirects"

# App mock backend
server s1 {
  # Test for /not-found
  rxreq
  expect req.url == "/not-found"
  txresp -status 404 -body "App backend"
} -start

setenv APP_HOST "app.local"
setenv BACKEND_APP_FQDN "app.local"
setenv BACKEND_APP_HOST "${s1_addr}"
setenv BACKEND_APP_PORT "${s1_port}"

setenv ASSETS_HOST "assets.local"

# Start varnish with our VCL
varnish v1 -arg "-s memory=malloc,10M" \
           -arg "-s disk=file,${tmpdir}/file.bin,10M" -vcl {
  vcl 4.1;

  import dynamic;
  import std;

  # Use an app backend with no probe when testing
  sub vcl_init {
    new app = dynamic.director(
      host_header = std.getenv("BACKEND_APP_FQDN"),
    );
  }

  include "app-backend.vcl";
  include "disable-default-backend.vcl";
  include "news-mp3.vcl";
} -start

# / should go to app backend simulating a page not found
client c1 {
  txreq -url "/not-found"
  rxresp
  expect resp.status == 404
  expect resp.body == "App backend"
} -run

# test x-redirect header ignore
client c2 {
  txreq -url "/uploads/podcast/news-2022-06-27/the-changelog-news-2022-06-27.mp3" -hdr "x-redirect: kaizen.com"
  rxresp
  expect resp.status == 308
  expect resp.http.location == "https://assets.local/uploads/news/1/changelog-news-1.mp3"
} -run

# test basic redirect returning different path and status code
client c3 {
  txreq -url "/uploads/podcast/news-2022-06-27/the-changelog-news-2022-06-27.mp3"
  rxresp
  expect resp.status == 308
  expect resp.http.location == "https://assets.local/uploads/news/1/changelog-news-1.mp3"
} -run

# test basic redirect returning different path and status code and including the query string
client c4 {
  txreq -url "/uploads/podcast/news-2022-06-27/the-changelog-news-2022-06-27.mp3?this=is&a=query&string"
  rxresp
  expect resp.status == 308
  expect resp.http.location == "https://assets.local/uploads/news/1/changelog-news-1.mp3?this=is&a=query&string"
} -run

# we use acceptance tests for the other redirects, so that we compare the behaviour of both CDNs
