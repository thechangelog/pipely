# vim: set tabstop=4 shiftwidth=4 expandtab:

set shell := ["bash", "-uc"]

[private]
default:
    @just --list

[private]
fmt:
    just --fmt --check --unstable
    just --version

# Start all processes
up:
    overmind start --timeout=30 --no-port --auto-restart=all

# Check $url
check url="http://localhost:9000":
    httpstat {{ url }}

# List Varnish backends
backends:
    varnishadm backend.list

# Tail Varnish backend_health
health:
    varnishlog -g raw -i backend_health

# Varnish top
top:
    varnishtop

# Varnish stat
stat:
    varnishstat -1

# Run VCL tests
test-vtc *ARGS:
    varnishtest {{ ARGS }} -j 8 test/vtc/*.vtc test/vtc/**/*.vtc

# Run acceptance tests
test-acceptance-local *ARGS:
    hurl --test --color --continue-on-error --report-html /var/opt/hurl/test-acceptance-local \
     --variable proto=http \
     --variable host=localhost:9000 \
     --variable assets_host=cdn.changelog.com \
     --variable delay_ms=6000 \
     --variable delay_s=5 \
     --variable purge_token="{{ env("PURGE_TOKEN") }}" \
     {{ ARGS }} \
     test/acceptance/*.hurl test/acceptance/local/*.hurl test/acceptance/production/*.hurl

# Show Varnish cache stats
cache:
    varnishstat -1 -f "MAIN.cache*" -f "MAIN.shm*" -f "SMA.*" -f "SMF.*"

[private]
bench url="http://localhost:9000/" http="2" reqs="1000" conns="50":
    time oha -n {{ reqs }} -c {{ conns }} {{ url }} --http-version={{ http }}

# Benchmark app origin
bench-app-1-origin: (bench "https://changelog-2025-05-05.fly.dev/")

# Benchmark app via Fly.io
bench-app-2-fly: (bench "https://changelog.com/" "2" "10000")

# Benchmark app via local
bench-app-3-local: (bench "http://localhost:9000/" "2" "10000")

# Benchmark app TLS proxy
bench-app-4-tls-proxy: (bench "http://localhost:5000/" "1.1")

# Benchmark feed origin
bench-feed-1-origin: (bench "https://feeds.changelog.place/podcast.xml")

# Benchmark feed via Fly.io
bench-feed-2-fly: (bench "https://changelog.com/podcast/feed")

# Benchmark feed via local
bench-feed-3-local: (bench "http://localhost:9000/podcast/feed" "2" "10000" "50")

# Benchmark feed TLS proxy
bench-feed-4-tls-proxy: (bench "http://localhost:5010/podcast.xml" "1.1")

# https://williamyaps.github.io/wlmjavascript/servercli.html

# Speedtest Los Angeles
speedtest-los-angeles:
    speedtest-go -s 9916

# Speedtest Denver
speedtest-denver:
    speedtest-go -s 9912

# Speedtest Chicago
speedtest-chicago:
    speedtest-go -s 11750

# Speedtest Toronto
speedtest-toronto:
    speedtest-go -s 9911

# Speedtest Ashburn
speedtest-ashburn:
    speedtest-go -s 6030

# Speedtest London
speedtest-london:
    speedtest-go -s 6032

# Speedtest Paris
speedtest-paris:
    speedtest-go -s 6027

# Speedtest Amsterdam
speedtest-amsterdam:
    speedtest-go -s 9913

# Speedtest Frankfurt
speedtest-frankfurt:
    speedtest-go -s 10010
