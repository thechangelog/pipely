# vim: set tabstop=4 shiftwidth=4 expandtab:

set shell := ["bash", "-uc"]

[private]
default:
    @just --list

[private]
fmt:
    just --fmt --check --unstable
    just --version

# Start all processes
up:
    overmind start --timeout=30 --no-port --auto-restart=all

# Check $url
check url="http://localhost:9000":
    httpstat {{ url }}

# List Varnish backends
backends:
    varnishadm backend.list

# Tail Varnish backend_health
health:
    varnishlog -g raw -i backend_health

# Varnish top
top:
    varnishtop

# Varnish stat
stat:
    varnishstat -1

# Run VCL tests
test-vtc *ARGS:
    varnishtest {{ ARGS }} -j 8 test/vtc/*.vtc test/vtc/**/*.vtc

# Run acceptance tests
test-acceptance-local *ARGS:
    hurl --test --color --continue-on-error --report-html /var/opt/hurl/test-acceptance-local \
     --variable proto=http \
     --variable host=localhost:9000 \
     --variable assets_host=cdn.changelog.com \
     --variable nightly_host=nightly.changelog.com \
     --variable delay_ms=6000 \
     --variable delay_s=5 \
     --variable purge_token="{{ env("PURGE_TOKEN") }}" \
     {{ ARGS }} \
     test/acceptance/*.hurl test/acceptance/local/*.hurl

# Show Varnish cache stats
cache:
    varnishstat -1 -f "MAIN.cache*" -f "MAIN.shm*" -f "SMA.*" -f "SMF.*"

[private]
bench url="http://localhost:9000/" reqs="1000" conns="50" host="":
    time oha -n {{ reqs }} -c {{ conns }} {{ if host != "" { "--host " + host } else { "" } }} {{ url }}

# Benchmark app origin
bench-app-1-origin: (bench "https://changelog-2025-05-05.fly.dev/")

# Benchmark app TLS proxy
bench-app-2-tls-proxy: (bench "http://localhost:5000/")

# Benchmark app via Fly.io
bench-app-3-fly: (bench "https://changelog.com/" "100000")

# Benchmark app via local
bench-app-4-local: (bench "http://localhost:9000/" "100000")

# Benchmark assets origin
bench-assets-1-origin: (bench "https://changelog.place/static/images/podcasts/podcast-original-f16d0363067166f241d080ee2e2d4a28.png")

# Benchmark assets TLS proxy
bench-assets-2-tls-proxy: (bench "http://localhost:5010/static/images/podcasts/podcast-original-f16d0363067166f241d080ee2e2d4a28.png")

# Benchmark assets via Fly.io
bench-assets-3-fly: (bench "https://cdn.changelog.com/static/images/podcasts/podcast-original-f16d0363067166f241d080ee2e2d4a28.png")

# Benchmark assets via local
bench-assets-4-local: (bench "http://localhost:9000/static/images/podcasts/podcast-original-f16d0363067166f241d080ee2e2d4a28.png" "100000" "50" "cdn.changelog.com")

# Benchmark feed origin
bench-feed-1-origin: (bench "https://feeds.changelog.place/podcast.xml")

# Benchmark feed TLS proxy
bench-feed-2-tls-proxy: (bench "http://localhost:5020/podcast.xml")

# Benchmark feed via Fly.io
bench-feed-3-fly: (bench "https://changelog.com/podcast/feed")

# Benchmark feed via local
bench-feed-4-local: (bench "http://localhost:9000/podcast/feed" "100000" "50")

# Benchmark nightly origin
bench-nightly-1-origin: (bench "https://changelog-nightly-2023-10-10.fly.dev/")

# Benchmark nightly TLS proxy
bench-nightly-2-tls-proxy: (bench "http://localhost:5030/")

# Benchmark nightly via Fly.io
bench-nightly-3-fly: (bench "https://nightly.changelog.com/")

# Benchmark nightly via local
bench-nightly-4-local: (bench "http://localhost:9000/" "100000" "50" "nightly.changelog.com")

# https://williamyaps.github.io/wlmjavascript/servercli.html

# Speedtest Los Angeles
speedtest-los-angeles:
    speedtest-go -s 9916

# Speedtest Denver
speedtest-denver:
    speedtest-go -s 9912

# Speedtest Chicago
speedtest-chicago:
    speedtest-go -s 11750

# Speedtest Toronto
speedtest-toronto:
    speedtest-go -s 9911

# Speedtest Ashburn
speedtest-ashburn:
    speedtest-go -s 6030

# Speedtest London
speedtest-london:
    speedtest-go -s 6032

# Speedtest Paris
speedtest-paris:
    speedtest-go -s 6027

# Speedtest Amsterdam
speedtest-amsterdam:
    speedtest-go -s 9913

# Speedtest Frankfurt
speedtest-frankfurt:
    speedtest-go -s 10010
